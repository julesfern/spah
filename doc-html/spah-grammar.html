<!DOCTYPE html>
<html>
  <head>
    <title>Spah - Appendix: SpahQL EBNF Grammar</title>
    
    <link rel="stylesheet" href="css/normalize.css" type="text/css" media="screen" />
    <link rel="stylesheet" href="css/master.css" type="text/css" media="screen" />
    <link rel="stylesheet" href="css/ir_black.css" type="text/css" media="screen" />
    
    <script type="text/javascript" src="js/highlight.pack.js"></script>
    <script type="text/javascript" src="js/jquery.1.5.2.min.js"></script>
    <script type="text/javascript">
      $(document).ready(function() {
        hljs.initHighlighting();
      });
    </script>
  </head>
  <body>
    
     <nav>
      <ul><li><a href="index.html">Spah: Stateful pages, not single pages.</a></li><li>Appendix: SpahQL EBNF Grammar</li><li><a href="src/index.html">API</a></li></ul>
     </nav>
     
     <article>
      <h1 id='appendix_spahql_ebnf_grammar'>Appendix: SpahQL EBNF Grammar</h1>

<pre><code>PathDelimiter ::= &quot;/&quot;;
PathWildcard ::= &quot;*&quot;;
ArrayDelimiter ::= &quot;,&quot;;
RangeDelimiter ::= &quot;..&quot;;
SingleQuote ::= &quot;&#39;&quot;;
DoubleQuote ::= &quot;\&quot;&quot;;
Negative ::= &quot;-&quot;;
DecimalPoint ::= &quot;.&quot;;
PropertyIdentifier ::= &quot;.&quot;;
RootScopeFlag ::= &quot;$&quot;;
SetStart ::= &quot;{&quot;;
SetEnd ::= &quot;}&quot;;
FilterStart ::= &quot;[&quot;;
FilterEnd ::= &quot;]&quot;;
BooleanTrue ::= &quot;true&quot;;
BooleanFalse ::= &quot;false&quot;;
StrictEquality ::= &quot;==&quot;;
RoughEquality ::= &quot;=~&quot;;
Inequality ::= &quot;!=&quot;;
GT ::= &quot;&gt;&quot;;
LT ::= &quot;&lt;&quot;;
LTE ::= LT, &quot;=&quot;;
GTE ::= GT, &quot;=&quot;;
SetEquality ::= &quot;}={&quot;;
DisjointSet ::= &quot;}!{&quot;;
JointSet ::= &quot;}~{&quot;;
Superset ::= &quot;}&gt;{&quot;;
Subset ::= &quot;}&lt;{&quot;;

(* Tokens *)

ComparisonOperator ::= StrictEquality | RoughEquality
						| Inequality | GT | LT | GTE | LTE
						| SetEquality | DisjointSet
						| JointSet | Superset | Subset;

Digit ::= &quot;0&quot; | &quot;1&quot; | &quot;2&quot; | &quot;3&quot; | &quot;4&quot; | &quot;5&quot; | &quot;6&quot; | &quot;7&quot; | &quot;8&quot; | &quot;9&quot; ;

AlphaChar ::= &quot;A&quot; | &quot;B&quot; | &quot;C&quot; | &quot;D&quot; | &quot;E&quot; | &quot;F&quot; | &quot;G&quot;
                     | &quot;H&quot; | &quot;I&quot; | &quot;J&quot; | &quot;K&quot; | &quot;L&quot; | &quot;M&quot; | &quot;N&quot;
                     | &quot;O&quot; | &quot;P&quot; | &quot;Q&quot; | &quot;R&quot; | &quot;S&quot; | &quot;T&quot; | &quot;U&quot;
                     | &quot;V&quot; | &quot;W&quot; | &quot;X&quot; | &quot;Y&quot; | &quot;Z&quot; | &quot;a&quot; | &quot;b&quot;
							| &quot;c&quot; | &quot;d&quot; | &quot;e&quot; | &quot;f&quot; | &quot;g&quot; | &quot;h&quot; | &quot;i&quot;
							| &quot;j&quot; | &quot;k&quot; | &quot;l&quot; | &quot;m&quot; | &quot;n&quot; | &quot;o&quot; | &quot;p&quot;
							| &quot;q&quot; | &quot;r&quot; | &quot;s&quot; | &quot;t&quot; | &quot;u&quot; | &quot;v&quot; | &quot;w&quot;
							| &quot;x&quot; | &quot;y&quot; | &quot;z&quot;;
AlphaNumChar ::= AlphaChar | Digit;

NumericLiteral ::= [Negative], Digit, {Digit}, [DecimalPoint, Digit, {Digit}];

SingleQuoteString ::= SingleQuote, {all characters - SingleQuote}, SingleQuote;
DoubleQuoteString ::= DoubleQuote, {all characters - DoubleQuote}, DoubleQuote;
StringLiteral ::= SingleQuoteString | DoubleQuoteString;
BooleanLiteral ::= BooleanTrue | BooleanFalse;

PrimitiveLiteral ::= NumericLiteral | StringLiteral | BooleanLiteral;

SetMember ::= PrimitiveLiteral | SelectionQuery;

SetArrayLiteral ::= SetStart, ( SetEnd |  {SetMember, ArrayDelimiter}, SetMember, SetEnd );
SetRangeLiteral ::= SetStart, SetMember, RangeDelimiter, SetMember, SetEnd;

SetLiteral ::= SetArrayLiteral | SetRangeLiteral;

SelectionQuery ::= [RootScopeFlag], PathComponent, {PathComponent};
PathComponent ::= PathDelimiter, [PathWildcard | (PropertyIdentifier, KeyName) | KeyName], {FilterQuery};

KeyName ::= AlphaNumChar, {AlphaNumChar};

FilterQuery ::= FilterStart, RunnableAssertion, FilterEnd;

RunnableSelection ::= SetLiteral | SelectionQuery;
RunnableAssertion ::= RunnableSelection, [ComparisonOperator, RunnableSelection];

SpahQL ::= RunnableAssertion;</code></pre>
     </article>
    
  </body>
</html>